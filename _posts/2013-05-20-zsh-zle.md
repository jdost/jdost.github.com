---
layout: post
title: Zsh & ZLE
tldr: The ZLE system of Zsh can be used to level up your shell
tags: software tools
comments: true
---
I have been working on my zsh configs ([Github Repo][]) and came across a pretty 
useful snippet:

[Github Repo]: http://github.com/jdost/config-shell/

{% highlight bash linenos linespans=rat %}
function rationalize_dot {
   local MATCH
   if [[ $LBUFFER =~ '(^|/| |      |'$'\n''|\||;|&)\.\.$' ]]; then
      LBUFFER+=/
      zle self-insert
      zle self-insert
   else
      zle self-insert
   fi
}
zle -N rationalize_dot
bindkey . rationalize_dot
bindkey -M isearch . self-insert
{% endhighlight %}

What's happening in this code is, first, the definition of a function
[`rationalize_dot`](#rat-1:10) that scans the [current command buffer](#rat-3)
(`$LBUFFER` to see if it ends with `..`.  If it does, it [will add a `/`](#rat-4) 
and then insert the character typed twice (this will make more sense in a moment).
If the `$LBUFFER` does not end with `..`, the character typed is only inserted once.
After the function definition is the integration with ZLE.  The [`zle -N 
rationalize_dot`](#rat-11) call registers the created function as a valid ZLE key 
action.  Then the [following line](#rat-12) binds the created function to execute 
whenever a `.` is typed and the following disables this binding when in search mode.

So now what does this stuff do?  When traversing your file tree, you can now type
`...` and it will expand as `../..` and each subsequent `.` typed will expand as
another level up the tree.  This has been **very** useful in my day to day usage of
the shell.  With this *inserting* works as desired, but what if I have done one too
many `.`s ?  So I wrote an `unrationalize_dot` ZLE function to perform the opposite
action as `rationalize_dot`.  (Tools that are extendable via programming are so
awesome).  So what I ended up with:

{% highlight bash linenos linespans=unrat %}
function unrationalize_dot {
   local MATCH # keep regex match from leaking into the environment
   if [[ $LBUFFER =~ '(^|/| |      |'$'\n''|\||;|&)\.\.$' ]]; then
      zle backward-delete-char
      zle backward-delete-char
      if [[ $MATCH =~ '^/' ]]; then
         zle backward-delete-char
      fi
   else
      zle backward-delete-char
   fi
}
zle -N unrationalize_dot
bindkey "^h" unrationalize_dot
bindkey "^?" unrationalize_dot
{% endhighlight %}

This does basically the same as the other, except it will use 
[`backward-delete-char`](#unrat-4:5) instead of `self-insert` and will delete one 
more character [if there is a leading `/` in the buffer](#unrat-6:8) (so `..` 
removes both and `../..` becomes just `..`).  The keys bound to are [`^h`](#unrat-14) 
and [`^?`](#unrat-15), one is for 'Delete' and the other 'Backspace'.

So just a little snippet to power up some of my development tools!
